{ _, op, root, generateId, guidFor,
} = require './util'
{ EventEmitter, ReadyEmitter,
} = require 'emitters'
{ Resource,
} = require './resource'

ko     = require 'knockout'
unwrap = ko.utils.unwrapObservable




/**
 * @class Root-class for all Models.
 * @extends EventEmitter
 * @mixes Resource
 */
class exports.Model extends EventEmitter
    # Apply Resource mixin
    Resource.mix this
    
    /**
     * Unique identifier for this object. Uniqueness is only guaranteed for this
     * pageload session; this value is safe to use in circumstances requiring numeric identity
     * (such as calculating a hashkey for a Map, or for DOM id attributes) but not when
     * global uniqueness is required.
     * @type String
     */
    __id__ : null
    
    /**
     * Attribute key used to uniquely identify this model instance. Note that
     * it should be expected that models sometimes have a null id prior to save,
     * as the server may be used to enforce uniqueness.
     * @type String
     */
    idAttribute : 'id'
    
    /**
     * Attribute data of the model. Individual properties are also aliased 
     * onto the model object itself if no property already exists.
     * @type Map<Key, ko.observable>
     */
    attributes : null
    
    /**
     * Maps attribute keys to sub-models.
     * @type Map<Key, ModelClass>
     */
    attributeTypes : null
    
    
    
    /**
     * @constructor
     */
    (attributes={}) ->
        @__id__     = generateId()
        attributes  = _.merge @defaults(), attributes
        @attributes = ko.mapping.fromJS attributes, @generateMapping()
        for k, v in @attributes
            this[k] ?= v unless k is '__ko_mapping__'
        this
    
    
    /**
     * Invoked to acquire default attribute values.
     * 
     * Note that *all* known attributes of the Model should be included -- even
     * if simply as `null` keys (even the `id` attribute!) -- so Knockout knows
     * to create the observables.
     * 
     * @returns {Object} Hash of default attribute values.
     */
    defaults: ->
        { id:null }
    
    /**
     * Gets the value of the id attribute for this model.
     * @returns {String} Instance's id.
     */
    getId: ->
        @get(@idAttribute or 'id') or null
    
    /**
     * @returns {Boolean} Whether the model has been saved to the server.
     */
    isNew: ->
        @getId()?
    
    /**
     * Clone this model.
     * 
     * @returns {Model} A new instance of this model, bearing the same attributes.
     */
    clone: ->
        ModelClass = @constructor
        # TODO: @attributes might contain objects or models that need recursive cloning.
        new ModelClass ko.mapping.toJS @attributes
    
    
    /* * * *  Knockout-Aware Accessors  * * * {{{ */
    
    /**
     * Gets the attribute value at `key`.
     * @returns {*} Value at `key`.
     */
    get: (key) ->
        if ko.isObservable val = @attributes[key]
            val()
        else
            val
    
    /**
     * Gets the attribute value at `key`, circumventing dependency tracking.
     * @returns {*} Value at `key`.
     */
    peek: (key) ->
        if ko.isObservable val = @attributes[key]
            unwrap val
        else
            val
    
    /**
     * Puts a single attribute value to `key`.
     * 
     * @protected
     * @param {String} key The key to set.
     * @param {*} val Value to set at `key`.
     * @returns {this}
     */
    _set: (key, val) ->
        return this unless key? and val is not void
        
        obs = @attributes[key]
        unless ko.isObservable obs
            throw new Error("Key '#key' is not observable!")
        obs val
        this
    
    /**
     * Sets attribute values, taking either a single (key, value)-pair, or a map of them.
     * 
     * @param {String|Object} key The key to set. If an object is supplied here, each key will be set with its value on the target object.
     * @param {*} [value] Value to set at `key`. Omit this if an object of KV-pairs was passed as `key`.
     * @returns {this}
     */
    set: (key, val) ->
        return this unless key?
        if key and _.isObject(key)
            values = key
        else
            values = { "#key": val }
        for key, value in values
            @_set key, value
        this
    
    /**
     * Sets the attribute value at `key` to `null`.
     * @returns {this}
     */
    unset: (key) ->
        obs = @attributes[key]
        unless ko.isObservable obs
            throw new Error("Key '#key' is not observable!")
        obs null
        this
    
    # }}}
    
    
    
    /**
     * Transforms the `attributeType` mapping object (keyname -> Model class) into a `ko.mapping`
     * options object.
     * @returns {Object} Knockout mapping object for all listed (key, type) pairs.
     */
    generateMapping: ->
        return that if @_mapping
        return {} unless @attributeTypes
        return @_mapping = _.reduce do
            @attributeTypes
            (mapping, ModelType, key) ~>
                id = ModelType::idAttribute or 'id'
                mapping[key] =
                    key: (data) ~>
                        # TODO: allow customization of id/key
                        dataId = if data instanceof Model then data.attributes[id] else data[id]
                        dataId = unwrap dataId if ko.isObservable dataId
                        # console.log "#{this}.mapping[ #key ].key!", 'data:', data, '->', dataId
                        dataId
                    
                    create: (opts) ~>
                        {data, parent} = opts
                        # console.log "#{this}.mapping[ #key ].create!", ModelType.name, 'data:', data, 'opts:', opts
                        ko.observable new ModelType data
                    
                    # FIXME: I'm definitely missing something as to how update is "supposed" to work.
                    update: (opts) ~>
                        {data, parent, target, observable} = opts
                        # console.log "#{this}.mapping[ #key ].update!", ModelType.name, 'data:', data, 'target:', target, 'obs:', observable, 'opts:', opts
                        if observable
                            observable data
                        else if target instanceof Model
                            ko.mapping.fromJS data, target.generateMapping()
                            target
                        else
                            new ModelType data
                mapping
            {}
    
    
    toJSON: ->
        json = ko.mapping.toJS @attributes
        for k, v in json
            delete json[k] unless v?
        json
    
    toString: ->
        cid       = @__id__
        id        = @attributes?[@idAttribute or 'id']
        id        = unwrap id if ko.isObservable id
        Class     = @constructor
        className = Class.displayName or Class.name
        "#className(cid=#cid, id=#id)"
    
    
    
    /* * * *  Class Methods  * * * {{{ */
    
    # Make the Model class-object itself an event emitter, so it can fire global events
    this import EventEmitter::
    
    
    /**
     * When Model is extended, copy over all class methods to the Subclass (including this).
     */
    @extended = (SubClass) ->
        SuperClass = this
        for own k, v in SuperClass
            SubClass[k] = v unless SubClass[k]
            # SubClass[k] = v unless _.contains <[ apply call constructor toString ]>, k
        SubClass.__super__ = SuperClass::
        SubClass
    
    # }}}



