_  = require 'underscore'
ko = require 'knockout'

{ GraphNodeData,
} = require '../graph-node-data'
{ GraphNode,
} = require '../graph-node'
{ TimeseriesMetric,
} = require '../../../data/metric'

Trait = require '../graph-node-trait'


/**
 * @class
 * @extends GraphNodeData
 */
class exports.TimeseriesNodeData extends GraphNodeData
    @registerType 'timeseries'
    
    defaults: ->
        metric : null
    
    attributeTypes: ->
        metric : TimeseriesMetric
    
    -> super ...



/**
 * @class
 * @extends GraphNode
 */
class exports.TimeseriesNode extends GraphNode
    @registerType 'timeseries'
    traits : [
        Trait.VIS_NODE
        Trait.REQUIRES_METRIC
        Trait.METRIC_CONTENT_DATA
        Trait.METRIC_TIMESERIES
        Trait.SVG
        Trait.LEAF
    ]
    
    -> super ...
    
    
    /**
     * Materialized timeseries data.
     * @type ko.computed<Array<[Date, Value]>>
     */
    timeseriesData: @computedRequires 'model.metric', (metric) ->
        metric.data()
    
    
    /**
     * Label determined from the following, in order
     *   options.label
     *   metric.defaultLabel
     *   '(no label)'
     * @type ko.computed<String>
     */
    shortLabel: @computedRequires 'model.options', (options) ->
        options.label?() or @model().metric()?.defaultLabel() or '(no label)'
    
    
    /**
     * Label determined from the following, in order
     *   options.label
     *   metric.defaultLongLabel
     *   '(no label)'
     * @type ko.computed<String>
     */
    label: @computedRequires 'model.options', (options) ->
        options.label?() or @model().metric()?.defaultLongLabel() or '(no label)'
