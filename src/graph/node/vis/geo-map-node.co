_  = require 'underscore'
ko = require 'knockout'
d3 = require 'd3'

{ GroupNode, GroupNodeData,
} = require '../group-node'
{ GeoMapMetric,
} = require '../../../data/metric'

Trait = require '../graph-node-trait'
ProjectColors = require '../../project-colors'


/**
 * @class
 * @extends GroupNodeData
 */
class exports.GeoMapNodeData extends GroupNodeData
    @registerType 'geo-map'
    
    defaults: ->
        options:
            projection: 'mercator'
    
    attributeTypes: ->
        metric: GeoMapMetric
    
    -> super ...



/**
 * @class
 * @extends GroupNode
 */
class exports.GeoMapNode extends GroupNode
    @registerType 'geo-map'
    traits : [
        Trait.VIS_NODE
        Trait.REQUIRES_METRIC
        Trait.METRIC_GEO_MAP
        Trait.SVG
    ]
    
    -> super ...
    
    
    features : ko.observable null
    
    render: ->
        return unless mapData  = @model().metric().data()
        return unless viewport = @viewport()
        return unless width    = viewport.width()
        return unless height   = viewport.height()
        
        projection = d3.geo.mercator()
            .scale width
            .translate viewport.center()
        
        path = d3.geo.path()
            .projection projection
        
        @root().background '#BFBFBF'
        
        features = @sel.selectAll '.feature'
            .data mapData.features
                .enter().append 'path'
        
        features
            .attr  'class',  'feature'
            .attr  'id',     (d) -> d.id
            .attr  'd',      path
            .style 'fill',   'white'
        
        @features features
    
    
    
