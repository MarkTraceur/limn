_  = require 'underscore'
ko = require 'knockout'
d3 = require 'd3'

{ TimeseriesNode,
} = require './timeseries-node'

Trait = require '../graph-node-trait'



/**
 * @class
 * @extends TimeseriesNode
 */
class exports.LineNode extends TimeseriesNode
    nodeType : 'line'
    traits   : [
        Trait.VIS_NODE
        Trait.REQUIRES_METRIC
        Trait.METRIC_TIMESERIES
        Trait.SVG
        Trait.LEAF
    ]
    @registerType()
    
    
    -> super ...
    
    buildElement: (parentElment) ->
        el = super ...
        # touch the line to start updates
        @line()
        el
    
    
    /**
     * Materialized timeseries data, Array-wrapped for d3.
     * @type ko.computed<Array<Array<[Date, Value]>>>
     */
    timeseriesData: @computed ->
        metric = @model()?.metric()
        data   = if metric?.data() then [that] else []
    
    
    /**
     * Update selection representing the SVG path element for the line.
     * @type ko.computed<d3.selection.update>
     */
    line: @computed ->
        return null unless @el() and @sel
        data = @timeseriesData()
        
        # create selection
        line = @selectAll 'path.metric-line' .data data
        
        # always exit() to remove existing nodes if data has changed and become invalid
        line.exit().remove()
        
        return line unless data.length
        return null unless model  = @model()
        return null unless scales = @root().scales()
        
        label   = model.get('label') or '(no label)'
        options = model.options()
        color   = options?.get('color') or 'skyblue'
        stroke  = options?.get('stroke') or 5
        
        line.enter()
            .append 'path'
                .classed 'metric-line', true
                .attr  'vector-effect', 'non-scaling-stroke'
                .style 'fill',          'none'
                .style 'stroke-width',  stroke
                .style 'stroke',        color
        line.transition()
            .attr 'd', scales.scaleLine
        line
    

