_  = require 'underscore'
ko = require 'knockout'
d3 = require 'd3'

{ TimeseriesNode, TimeseriesNodeData,
} = require './timeseries-node'

Trait = require '../graph-node-trait'
ProjectColors = require '../../project-colors'



/**
 * @class
 * @extends TimeseriesNodeData
 */
class exports.LineNodeData extends TimeseriesNodeData
    @registerType 'line'
    
    defaults: ->
        options :
            disabled    : false
            label       : null
            stroke      : null
            noLegend    : false
    
    attributeTypes: ->
        {}
    
    -> super ...



/**
 * @class
 * @extends TimeseriesNode
 */
class exports.LineNode extends TimeseriesNode
    @registerType 'line'
    traits : [
        Trait.VIS_NODE
        Trait.REQUIRES_METRIC
        Trait.METRIC_CONTENT_DATA
        Trait.METRIC_TIMESERIES
        Trait.SVG
        Trait.LEAF
    ]
    
    
    ->
        super ...
        @tooManyDataPoints = ko.observable false
        @trackHover()
    
    
    trackHover: @computed ->
        return if @tooManyDataPoints()
        return unless cursor = @root().cursor()
        return unless @model().metric()?.data()?.length
        return unless @sel
        
        @trackingCircle ?= @sel.append 'circle'
            .classed 'tracking', true
            .style do
                fill: @strokeColor()
            .attr do
                r: 5
        
        cx = -10
        cy = -10
        
        if cursor.hovering and not @disabled()
            date = @root().xScale.invert cursor.x
            closest = @model().metric().findClosest date
            
            cx = @root().xScale closest.date
            cy = @root().yScale closest.value
            
            unless isFinite cx and isFinite cy
                cx = -10
                cy = -10
        
        @trackingCircle.attr do
            cx: cx
            cy: cy
    
    paletteColor: @computedRequires 'model.options', (options) ->
        ProjectColors.lookup(@shortLabel(), options.get('palette'))
    
    strokeColor: @computedRequires 'model.options', (options) ->
        options.stroke?()?.color or @paletteColor() or 'black'
    
    width: @computedRequires 'model.options', (options) ->
        stroke = options.get 'stroke'
        stroke.width?() or 3
    
    dasharray: @computedRequires 'model.options', (options) ->
        options.stroke?()?.dashed?() or 'none'
    
    disabled: @computedRequires 'model.options', (options) ->
        options.disabled?() or false
    
    /**
     * Update selection representing the SVG path element for the line.
     * @type ko.computed<d3.selection.update>
     */
    render: ->
        data = if @timeseriesData() then [that] else []
        
        return null unless data.length
        return null unless model  = @model()
        return null unless xScale = @viewport().xScale()
        
        # TODO: do not use features before they are implemented
        # dot separated options do not work
        # shape = @parent().get('options.shape') or 'line'
        shape = @parent().options().shape?() or 'line'
        color = @strokeColor()
        
        line = @selectAll 'path.metric-line' .data data
        
        # optimization: if there are more data points than pixels in the range,
        # set a flag and use it to do things like not show tracking circles
        xRange = xScale.range()
        @tooManyDataPoints data[0].length > (2 * Math.abs(xRange[0] - xRange[1]))
        
        # always exit() to remove existing nodes if data has changed and become invalid
        line.exit().remove()
        
        path = line.enter()
            .append 'path'
                .classed 'metric-line',     true
                .attr  'vector-effect',     'non-scaling-stroke'
                .style 'fill',              'none'
                .style 'stroke-dasharray',  @dasharray()
                .style 'stroke-width',      @width()
                .style 'stroke',            color
        
        if shape is 'area'
            path
                .style 'fill',              color
                .style 'opacity',           0.4
        
        line.style 'visibility', if @disabled() then "hidden" else "visible"
        line.transition()
            .attr 'd', @viewport().scaleShape(shape)
        
        line
    
    
