_  = require 'underscore'
ko = require 'knockout'
d3 = require 'd3'

{ GraphNode,
} = require '../graph-node'

Trait = require '../graph-node-trait'



/**
 * @class
 * @extends GraphNode
 */
class exports.AxisNode extends GraphNode
    nodeType : 'axis'
    traits   : [
        Trait.META_NODE
        Trait.FG_LAYER
        Trait.SVG
        Trait.LEAF
    ]
    @registerType()
    
    
    -> super ...
    
    
    render: ->
        options = @model().options()
        return unless scales    = @root().scalesIfValid()
        return unless dimension = options.get('dimension')
        return unless scale     = scales[ dimension+'Scale' ]
        
        orient = options.get('orient')
        ticks  = options.get('ticks')
        axis   = d3.svg.axis().scale scale
        
        switch dimension
        case 'x'
            axis.tickFormat @dateFormatterFor 'MMM YY'
                .orient(orient or= 'bottom')
            axis.ticks d3.time.months, ticks if ticks > 0
        case 'y'
            axis.tickFormat @numberFormatterFor 2
                .orient(orient or= 'left')
            axis.ticks ticks if ticks > 0
        
        @sel.classed dimension+'-axis axis', true
        @sel.call axis
        @moveAxis()
        
        @zoomHandler ?= @root().handle 'zoom-graph', this, (translate, scale) ~>
            # TODO: this does dark magic, figure out why it's broken
            @render()
        
        axis
    
    
    moveAxis: @computed ->
        return unless el = @el() and @sel
        @sel.attr 'transform', ''
        
        # TODO: handle other orientations
        return unless @model()?.options()?.get('dimension') is 'x'
        height = @root().viewportHeight()
        @sel.attr 'transform', "translate(0, #height)"
        height
    

