_  = require 'underscore'
ko = require 'knockout'
d3 = require 'd3'

{ GraphNode,
} = require '../graph-node'

Trait = require '../graph-node-trait'



/**
 * @class
 * @extends GraphNode
 */
class exports.ZoomBrushNode extends GraphNode
    nodeType : 'zoom-brush'
    traits   : [
        Trait.VIS_NODE
        Trait.SVG
        Trait.LEAF
    ]
    @registerType()
    
    
    -> super ...
    
    
    render: ->
        return unless root = @root()
        return unless scales = root.scalesIfValid()
        
        return if @brush
        
        @brush = d3.svg.brush()
            .x scales.xScale
            .y scales.yScale
            .on 'brushend', ~>
                @previousXDomain = @root().xScale.domain()
                @previousYDomain = @root().yScale.domain()
                @zooming.call this
        root.sel.select 'svg'
            .on 'dblclick', ~>
                @hideTrackingCircles()
                @root().changeDomains @previousXDomain, @previousYDomain
                @$el .toggle true
                window.getSelection?().removeAllRanges()
                document.selection?.empty()
        
        @sel.call @brush
        @sel.selectAll 'rect'
            .attr 'height', root.viewportHeight()
    
    
    zooming: ->
        extent = d3.event.target.extent()
        if @extentIsSignificant extent
            @$el .toggle false
            @root().changeDomains [extent[0][0], extent[1][0]], [extent[0][1], extent[1][1]]
        @brush.clear()
        @sel.call @brush
        @hideTrackingCircles()
    
    hideTrackingCircles: ->
        @root().sel.selectAll 'circle.tracking'
            .attr do
                cx: -10
                cy: -10
    
    extentIsSignificant: (extent) ->
        moment(extent[1][0]).diff(moment(extent[0][0]), 'months') > 1
