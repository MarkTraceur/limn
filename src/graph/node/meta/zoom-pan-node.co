_  = require 'underscore'
ko = require 'knockout'
d3 = require 'd3'

{ GraphNode,
} = require '../graph-node'

Trait = require '../graph-node-trait'



/**
 * @class
 * @extends GraphNode
 */
class exports.ZoomPanNode extends GraphNode
    nodeType : 'zoom-pan'
    traits   : [
        Trait.VIS_NODE
        Trait.SVG
        Trait.LEAF
    ]
    @registerType()
    
    
    -> super ...
    
    
    render: ->
        return unless root = @root()
        return unless scales = root.scalesIfValid()
        
        return if @behavior
        
        @behavior = d3.behavior.zoom().scaleExtent [1,10]
        @behavior.on 'zoom', ~> @zooming.call this
        
        @behavior.x scales.xScale
        @behavior.y scales.yScale
        
        d3.select('svg').call @behavior
    
    
    zooming: ->
        [tx, ty] = translate = d3.event.translate
        scale = d3.event.scale
        frame = d3.select 'g.content-frame'
        
        # special case, if they zoom all the way out, bring them back to normal
        if scale is 1
            [tx, ty] = translate = [0,0]
            @behavior.translate(translate)
            @behavior.scale(scale)
            frame = frame.transition(100)
        
        frame.attr "transform", "translate(#tx,#ty) scale(#scale)"
        
        unless scale is 1
            @root().zooming translate, scale
    

