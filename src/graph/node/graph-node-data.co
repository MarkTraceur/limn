_  = require 'underscore'
ko = require 'knockout'

{ compareIds, OrderedSet,
} = require '../../util'
{ Model,
} = require '../../base'
{ Metric,
} = require '../../data/metric-model'



class exports.GraphNodeData extends Model
    
    
    defaults: ->
        nodeType       : 'group'
        label          : null
        disabled       : false
        metric         : null
        options        : {}
        children       : []
    
    attributeTypes :
        metric   : Metric
        children : GraphNodeData
    
    
    /**
     * Parent of this node.
     * @type ko.observable<GraphNodeData>
     */
    parent : @eagerCoerciveObservable GraphNodeData
    
    
    /**
     * Ensure children Arrays are properly updated when parent changes.
     * 
     * Note: Knockout ignores cycles in deps, so the circular modification
     * trigger between `parent` and `children` is benign.
     */
    watchParent : @computed ->
        oldParent = @parent.prev()
        oldParent?.children.remove this
        newParent = @parent()
        newParent?.children.push this
        newParent
    
    
    /**
     * Ensure parents are properly set when child-nodes are added or removed.
     * 
     * Note: Knockout ignores cycles in deps, so the circular modification
     * trigger between `parent` and `children` is benign.
     */
    watchChildren : @computed ->
        oldChildren = @children.prev()
        newChildren = @children()
        
        # Unparent children being removed (not in the new set)
        _.each oldChildren, (child) ~>
            child.parent(null) if child.parent.peek() is this and not _.contains newChildren, child
        
        # Set the parent of objects that are children
        _.invoke newChildren, 'parent', this
        
        newChildren
    
    
    /**
     * @constructor
     */
    (attributes={}) ->
        super()
        
        # Decorate properties we need to clean up with history tracking
        @parent   .= history({ -includeInitial })
        @children  = @attributes.children = @children.history({ -includeInitial })
        @parent.equalityComparer = @children.equalityComparer = compareIds
        
        # Finally, kick off attribute updates
        @set attributes
        
        # ...And begin watching relevant properties
        @watchParent()
        @watchChildren()
    
    


