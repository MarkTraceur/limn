_  = require 'underscore'
ko = require 'knockout'

limn = require '../' # yes, you actually need the trailing slash
{ View,
} = require '../base/view'
{ OrderedMap,
} = require '../util'
{ Graph,
} = require './graph-model'
{ TableView,
} = require './table-view'
{ GraphNode,
} = require './node/graph-node'
{ EditView,
} = require '../edit/edit-view'

Trait = require './node/graph-node-trait'



class exports.GraphView extends View
    template: 'graph'
    
    defaults: ->
        model  : null
        action : null
        isDisposed : false
    
    
    /**
     * @constructor
     * @param {Graph} model Graph model for this view.
     * @param {string} action the action this view was created to respond to
     */
    (model, action) ->
        @nodeCache = new OrderedMap()
        super()
        @graphId = 'graph-' + @__id__
        
        if model instanceof Graph
            model.fetch()
        else if typeof model is 'string'
            model = Graph.load { id:model }
        else
            model = new Graph model
        
        @action action
        @model model
        @tabularizeDialog = ko.observable()
        
        @watching()
    
    /**
     * The `GraphNode` cache.
     * @protected
     * @type OrderedMap<GraphNodeData, GraphNode>
     */
    nodeCache : null
    
    /**
     * root to the tree of GraphNode(s)
     * @type GraphNode
     */
    rootNode : @eagerComputed ->
        return null unless graph = @model()
        @lookupNode graph.root()
    
    /**
     * DOM Element for this view. jQuery objects and d3 Selections
     * will be automatically stripped of their wrappers.
     * @type ko.observable<Element>
     */
    el : @eagerCoerciveObservable (el) ->
        @$el = null
        el = el[0]      if el instanceof jQuery
        el = el.node()  if el instanceof d3.selection
        return el       unless _.isElement el
        @$el = $ el
        el
    
    /**
     * jQuery-wrapped root element.
     * @type jQuery
     */
    $el : null
    
    
    /**
     * 
     */
    watchDOM: @computed ->
        node = @rootNode()
        el   = @el()
        return null unless limn?.domReady() and node and el
        return null unless nodeParentEl = @$('.graph-canvas-row > .inner').0
        node.build nodeParentEl
        nodeParentEl
    
    
    /**
     * Updates our wrappers for markdown fields when the model changes.
     */
    updateMarkdownFields: @eagerComputed ->
        return null unless model = @model()
        @descMarkdown  = model.desc.markdown()
        @notesMarkdown = model.notes.markdown()
        model
    
    
    
    /**
     * Inform sub-objects its safe to begin their watchers.
     */
    watchOthers : ->
        rootNode    = @rootNode().watching()
        rootBuilt   = @watchDOM()
        [ rootNode, rootBuilt ]
    
    
    /**
     * Looks up a `GraphNode` by an instance of `GraphNodeData`. On cache-miss,
     * creates a new GraphNode for the data and registers it with the cache.
     * 
     * @protected
     * @param {GraphNodeData} nodeData
     * @returns {GraphNode} The node corresponding to this data.
     */
    lookupNode: (nodeData) ->
        return null unless nodeData?
        return nodeData if nodeData instanceof GraphNode
        return that if @nodeCache.get nodeData
        
        node = GraphNode.fromNodeData this, nodeData
        @nodeCache.set nodeData, node
        
        # register for notification when the node is destroyed
        destroySub = node.isDisposed.subscribe (isDisposed) ~>
            throw new Error "WTF! node.isDisposed false for #node!" unless isDisposed
            currentNode = @nodeCache.get nodeData
            @nodeCache.remove nodeData if currentNode is node
            destroySub.dispose()
        
        # inform the node it can begin watching
        node.watching() if @isWatching
        
        node
    
    
    /**
     * Determin the DOM node to act as the parent the given node.
     * 
     * @protected
     * @param {GraphNode} node
     * @returns {Element}
     */
    determineDOMParentForNode: (node, parentNode) ->
        return @determineDOMParentForChildNode(node) unless parentNode
        
        elementType = if _.contains(node.traits, Trait.SVG) then Trait.SVG else Trait.HTML
        return parentNode.el() if _.contains parentNode.traits, elementType
        unless grandparentNode = parentNode.parent()
            throw new Error "Unable to determine DOM parent for child node! #node"
        @determineDOMParentForChildNode node, grandparentNode
    
    
    /**
     * Determine the DOM node to act as the parent for a direct child of the root node.
     * 
     * @protected
     * @param {GraphNode} node
     * @returns {Element}
     */
    determineDOMParentForChildNode: (node) ->
        # SVG nodes
        if node.hasTrait Trait.SVG
            if node.hasTrait Trait.FG_LAYER
                @$ 'svg > g > g.frame > g.fg' .0
            else if node.hasTrait Trait.BG_LAYER
                @$ 'svg > g > g.frame > g.content-frame > g.bg' .0
            else
                @$ 'svg > g > g.frame > g.content-frame > g.viewport' .0
        
        # HTML nodes
        else
            # XXX: Hack
            if node.hasTrait Trait.CALLOUT_NODE
                @$ '.graph-name-row > section.callout' .0
            else if node.hasTrait Trait.SECTION
                @$el.parent().0
            else if node.hasTrait Trait.FG_LAYER
                @$ 'section.meta.fg' .0
            else
                @$ 'section.meta.bg' .0
    
    
    resize: ->
        @rootNode?.peek()?.resize()
    
    
    /**
     * Called by Knockout once the Graph template has finished rendering.
     */
    afterRender: (element) ->
        # console.log "#this.afterRender()"
        @el element
        
        if @action() is 'edit'
            new EditView this
    
    
    uniqueMetricDataLinks: @computed ->
        links = @rootNode()?.walk [], (links, node) ->
            if      node.hasTrait(Trait.METRIC_CONTENT_DATA) and
                    url = node.model()?.metric()?.source()?.url()
                links.push url
            links
        _.uniq links, false
    
    
    tabularize: ->
        @tabularizeDialog new TableView @model(), @rootNode()
        @$el.find '.tabularizeDialog' .modal 'show'
