_   = require 'underscore'
op  = require 'operator'
ko  = require 'knockout'
Seq = require 'seq'

{ ReadyEmitter,
} = require 'emitters'

{ Model, StoredModel, ModelCache,
} = require '../../base'
{ ParsingMixin,
} = require '../../util'
{ Dataset,
} = require '../dataset'




/**
 * @class Encapsulates the definition of a column in a DataSource.
 */
class exports.ColumnDef extends Model
    ParsingMixin.mix this
    
    (attributes, @source) -> super ...
    
    defaults: ->
        id    : null
        label : ''
        type  : 'int'
        index : -1
    
    /**
     * Convert the string representation of a value from this Column to
     * its real type.
     * 
     * @param {String} value A serialized value.
     * @returns {*} The value parsed into the appropriate type.
     */
    parse: (value) ->
        @parseValue value, @get 'type'
    


/**
 * @class Metadata representing a source of data, such as a CSV file or
 * a web service endpoint.
 */
class exports.DataSource extends StoredModel
    -> super ...
    
    
    resource : 'datasources'
    getId    : -> @get 'id'
    
    formatContentTypes:
        # format    : Content-Type for AJAX request
        csv         : 'text'
        tsv         : 'text'
        json        : 'json'
        jsonp       : 'jsonp'
        xml         : 'xml'
    
    defaults: ->
        id            : null
        slug          : null
        
        format        : 'json'          # := csv | tsv | json | jsonp | xml                 ContentType/serialization/encoding of the dataset response.
        type          : 'timeseries'    # := series | timeseries | geojson | topojson                 Type of the contents of the dataset.
        url           : ''
        
        name          : ''
        shortName     : ''
        desc          : ''
        notes         : ''
        
        columns       : []
        timespan      : { start:null, end:null, step:null }
    
    attributeTypes: ->
        columns: ColumnDef
    
    
    
    /**
     * Whether this DataSource is valid, and thus ready to load data.
     * @type ko.computed<Boolean>
     */
    isValid: @computed ->
        !!  @dataUrl() and @get('format') in @formatContentTypes
    
    
    /**
     * Generates a random number of length around 10
     */
    generateRandom: -> 999999999 + Math.floor(Math.random() * 999999999)
    
    
    /**
     * URL for the data.
     * @type ko.computed<String>
     */
    dataUrl: @computedRequires 'url', (url) ->
        return url unless url
        if url.indexOf('http') == 0
            # TODO: no need to stream this through proxy, use CORS instead
            url = "/x/#url"
        else
            cacheBuster = @generateRandom()
            url = "#url?#cacheBuster"
    
    
    /**
     * Encoding of the dataset response.
     * @type ko.computed<String>
     */
    dataFormat: @computed ->
        format = @format()
        unless format
            return @defaults.format unless @url()
            @determineFormat()
        format
    
    
    determineFormat: ->
        fourBeforeEnd = @url().length - 4
        if @url().indexOf('.csv') is fourBeforeEnd
            @format 'csv'
        else if @url().indexOf('.json') is fourBeforeEnd - 1
            @format 'json'
        else if @url().indexOf('.tsv') is fourBeforeEnd
            @format 'tsv'
        else if @url().indexOf('.xml') is fourBeforeEnd
            @format 'xml'
        else # default - parses as text and allows pre-parsing
            @format 'log'
    
    
    /**
     * Dataset class wrapping the data.
     * @type ko.computed<Class<Dataset>>
     */
    datasetType: @computedRequires 'type', (type) ->
        Dataset.lookupType type
    
    
    /**
     * The content-type of the datafile, as sent by the server; null before load.
     * @type ko.observable<String>
     */
    dataContentType : ko.observable null
    
    
    /**
     * The reified dataset associated with this DataSource.
     * @type ko.asyncComputed<TimeSeriesData|CSVData>
     * @depends url, format
     */
    data: @asyncComputed ->
        return unless @isValid()
        return unless format      = @dataFormat()
        return unless DatasetType = @datasetType()
        
        xhr = $.ajax do
            url      : @dataUrl()
            dataType : @formatContentTypes[format]
        
        promise = xhr.pipe (data) ~> new DatasetType this, data
        promise.then do
            (data) ~>
                @dataContentType xhr.getResponseHeader 'Content-Type'
                @trigger 'data-load',  this, data
            (err) ~>
                @dataContentType null
                @trigger 'data-error', this
        promise.xhr = xhr
        
        promise
    
    
    /**
     * Enforces backwards compatibility at runtime by converting a DataSource from
     * one of many of the historical "formats" we've used to whatever the modern
     * format looks like.
     * 
     * @param {Object} data Raw DataSource attributes to canonicalize.
     * @returns {Object} Converted raw data.
     */
    canonicalize: (data) ->
        data.slug         or= data.id
        data.shortName    or= data.name
        
        cols = data.columns
        if _.isArray cols
            data.columns = _.map cols, (col, idx) ->
                if _.isArray col
                    [label, type] = col
                    id = _.str.underscored(label.toLowerCase())
                    {id, index: idx, label, type or 'int'}
                else
                    col.type or= 'int'
                    col.index or= idx
                    col
        else
            cols = _.merge { ids:[], types:[] }, cols
            data.columns = _.map cols.labels, (label, idx) ->
                id   = cols.ids[idx]   or _.str.underscored(label.toLowerCase())
                type = cols.types[idx] or 'int'
                {id, index: idx, label, type}
        data
    
    
    /**
     * Looks up a column definition.
     * 
     * @param {Number|String} id Identifier of the column, by index or id.
     * @returns {ColumnDef}
     */
    getColumnDef: (id) ->
        if typeof id is 'number'
            @columns()?[id]
        else
            _.find @columns(), -> it.id.peek() is id
    
    

