_  = require 'underscore'
ko = require 'knockout'

{ Model, StoredModel, ModelCache,
} = require '../base'
{ Query, ParsingMixin,
} = require '../util'
{ DataSource,
} = require './datasource-model'


class exports.Metric extends Model
    ParsingMixin.mix this
    
    
    (attributes) -> super ...
    
    
    defaults : ->
        source_id  : null       # String
        source_col : null       # String|Number
        type       : 'int'      # String
        timespan : 
            start  : null       # String -> Date
            end    : null       # String -> Date
            step   : null       # String -> moment.Duration
    
    
    /**
     * Data source of the Metric.
     * @type ko.computed<DataSource>
     * @depends source_id
     */
    source : @asyncComputed ->
        DataSource.lookup id if id = @get 'source_id'
    
    
    /**
     * Query object for the parameters of this Metric.
     * @type ko.computed<Query>
     * @depends source_col, timespan
     */
    query : @computed ->
        source_col = @get 'source_col'
        timespan   = @get 'timespan'
        return unless source_col? and timespan
        Query()
            .step       timespan.step()
            .timespan   timespan.start(), timespan.end()
    
    
    /**
     * The reified dataset (as (date, value) pairs) associated with this Metric.
     * @type ko.computed<[Date, Value]>
     * @depends source, query
     */
    data : @computed ->
        return unless query      = @query()
        return unless csv        = @source()?.data()
        return unless source_col = @get 'source_col'
        query
            .rows true
            .columns 0, source_col
            .apply csv.getData()
    
    
    /**
     * The reified dates for the dataset associated with this Metric.
     * @type ko.computed<Array<Date>>
     * @depends source, query
     */
    dates : @computed ->
        return unless query = @query()
        return unless csv   = @source()?.data()
        query
            .rows false
            .columns null
            .call csv.getDateColumn()
    
    
    /**
     * The reified dataset values associated with this Metric.
     * @type ko.computed<Array<Value>>
     * @depends source, query, source_col
     */
    values : @computed ->
        source_col = @get 'source_col'
        return unless source_col?
        return unless query = @query()
        return unless csv   = @source()?.data()
        query
            .rows false
            .columns source_col
            .apply csv.getData()
    
    
    /**
     * The reified dataset associated with this Metric.
     * @type ko.computed<ColumnDef>
     * @depends source, source_col
     */
    columnDef : @computed ->
        source_col = @get 'source_col'
        return unless source_col?
        @source()?.columns()?[source_col]
    
    
    /**
     * The reified dataset associated with this Metric.
     * @type ko.computed<String>
     * @depends columnDefinition
     */
    defaultLabel : @computed ->
        @columnDef()?.get 'label'
    
    

